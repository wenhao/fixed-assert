buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath(
                "org.springframework.boot:spring-boot-gradle-plugin:$springBootGradlePluginVersion",
                "net.saliman:gradle-cobertura-plugin:$coberturaPluginVersion",
                'se.transmode.gradle:gradle-docker:1.2'
        )
    }
}

group = "com.thoughtworks"
version = "1.0.0-SNAPSHOT"

allprojects {
    apply plugin: 'idea'
}

ext {
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        bootRunCommandLine = ['gradlew.bat', 'bRun']
        jasmineNodeCommandLine = ['cmd', '/c', 'jasmine-node', 'src/test/specs']
        shutdownCommandLine = ['cmd', '/c', 'curl', '-d', '123', 'localhost:8080/shutdown']
    } else {
        bootRunCommandLine = ['./gradlew', 'bRun']
        jasmineNodeCommandLine  = ['jasmine-node', 'src/test/specs']
        shutdownCommandLine = ['curl', '-d', '123', 'localhost:8080/shutdown']
    }
}
idea {
    project {
        jdkName = JavaVersion.VERSION_1_7
        languageLevel = JavaVersion.VERSION_1_7

        vcs = "Git"
    }
}

task shutdown(type: Exec) {
    commandLine shutdownCommandLine
}

task APItest(type:Exec) {
    workingDir 'api'
    commandLine  jasmineNodeCommandLine

    doFirst{
        //gradlew bootRun
        def builder = new ProcessBuilder(bootRunCommandLine)
        builder.redirectErrorStream(true)
        builder.directory(new File("."))
        def process = builder.start()
        //
        def line
        def reader = new BufferedReader(new InputStreamReader(process.getInputStream()))
        while ((line = reader.readLine()) != null) {
            logger.quiet line
            if (line.contains("Started ApplicationRunner")) {
                logger.quiet "Started ApplicationRunner is ready."
                break;
            }
        }
    }
    doLast{
        shutdown.exec()
    }
}

defaultTasks 'clean', 'cobertura', 'coberturaCheck', 'build'

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}